plugins {
  id 'java'
  id 'eclipse-wtp'
  id 'org.springframework.boot' version '2.7.7'
  id 'io.spring.dependency-management' version '1.1.0'
  id 'checkstyle'
  id 'jacoco'
}

sourceCompatibility = 1.11
targetCompatibility = 1.11

repositories {
  mavenLocal()
  mavenCentral()
}

group 'it.francescofiora'
version '1.0-SNAPSHOT'

def logback_version = property('logback.version')
def lombok_version = property('lombok.version')

dependencies {

  implementation 'org.springframework.boot:spring-boot-starter-batch'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

  implementation "ch.qos.logback:logback-classic:$logback_version"
  implementation 'com.h2database:h2'

  implementation "org.projectlombok:lombok:${lombok_version}"
  annotationProcessor "org.projectlombok:lombok:${lombok_version}"

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.batch:spring-batch-test'
  testAnnotationProcessor "org.projectlombok:lombok:${lombok_version}"
}

springBoot {
  mainClass.set("it.francescofiora.Application")
}

sourceSets {
  main {
    java {
      srcDirs 'java'
    }
    resources {
      srcDirs 'resources'
    }
  }
  test {
    java {
      srcDirs 'java'
    }
    resources {
      srcDirs 'resources'
    }
  }
}

checkstyle {
  toolVersion property('checkstyle.version')
  configFile  file("config/checkstyle/checkstyle.xml")
}
tasks.withType(Checkstyle) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}
checkstyleMain {
  source ='src/main/java'
}

test {
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}

jacocoTestReport {
  dependsOn test
  reports {
    xml.enabled true
    csv.enabled false
    html.destination file("${buildDir}/reports/jacoco/test")
    xml.destination  file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

wrapper {
  gradleVersion = "7.0"
}
